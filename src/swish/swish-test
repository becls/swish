#! /usr/bin/env swish

;;; Copyright 2018 Beckman Coulter, Inc.
;;;
;;; Permission is hereby granted, free of charge, to any person
;;; obtaining a copy of this software and associated documentation
;;; files (the "Software"), to deal in the Software without
;;; restriction, including without limitation the rights to use, copy,
;;; modify, merge, publish, distribute, sublicense, and/or sell copies
;;; of the Software, and to permit persons to whom the Software is
;;; furnished to do so, subject to the following conditions:
;;;
;;; The above copyright notice and this permission notice shall be
;;; included in all copies or substantial portions of the Software.
;;;
;;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
;;; EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
;;; MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
;;; NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
;;; HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
;;; WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
;;; OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;;; DEALINGS IN THE SOFTWARE.

(define who (path-last (car (command-line))))
(define (fail fmt . args) (apply errorf who fmt args))

(define help-sections '(all details examples))

(define shared-cli
  (cli-specs
   ["libdirs" -L (list "<libdir>") "add <libdir> to library-directories"]
   ["srcdirs" -s (list "<srcdir>") "add <srcdir> to source-directories"]
   ["help" -h --help (list "[<section>]" ...)
    (format "display help, <section>={狺撄}" help-sections)
    (usage hide)]
   ["version" --version bool "print version information"]))

(define misc-cli
  (cli-specs
   ["incl-tags" --tag (list "<tag>") "run only the tests with this tag"]
   ["excl-tags" --not (list "<tag>") "exclude tests with this tag"]
   ["load-prof" --load-profile (list "<file>" "<file>" ...)
    "load the specified profile(s)"]
   ["save-prof" --save-profile (string "<file>")
    "save profile data to specified file"]
   ["progress" --progress (string "<mode>") "<mode>={test|suite|none}"]
   ["report" --report (string "<file>") "write HTML report to <file>"]
   ["coverage" --coverage (string "<file>") "write coverage report to <file>"]
   ["exclude" --exclude (list "<glob>")
    "exclude these files from coverage report"]
   ["include" --include (list "<glob>") "override exclude for coverage report"]))

(define testing-cli
  (append misc-cli
    (cli-specs
     ["specs" (list . "<spec>") "directory | suite [{test | -t test} ...]"])))

(define repl-cli
  (cli-specs
   ["repl" --repl bool "start a repl" (usage show req)]
   ["files" (list "<file>" ...) "load remaining arguments"]))

(define any-cli (append shared-cli testing-cli repl-cli))

(define swish (osi_get_executable_path))
(define swish-libdir (path-parent swish))
(library-directories
 (cons (path-combine swish-libdir "lib")
   (library-directories)))

(define (wrap indent . texts)
  (wrap-text (current-output-port)
    (- (help-wrap-width) indent) indent indent (join texts #\space)))

(define-syntax example
  (syntax-rules ()
    [(_ (call ...) text ...)
     (begin
       (wrap 2 call ... "\n")
       (wrap 4 text ... "\n"))]))

(define-syntax any-sections
  (syntax-rules ()
    [(_ requested [(key ...) body ...] ...)
     (let ([sections requested])
       (define thunks
         (remq #f
           (list
            (and (ormap (lambda (x) (memq x sections)) '(key ...))
                 (lambda () body ...))
            ...)))
       (do ([ps thunks (cdr ps)] [sep "" "\n"]) ((null? ps))
         (display sep)
         ((car ps))))]))

(define (usage short? sections ht)
  (define selected
    (cond
     [(pair? sections) sections]
     [short? '(usage)]
     [else '(usage help)]))
  (any-sections selected
    [(all usage)
     (display-usage "Usage:" who (append shared-cli testing-cli))
     (display-usage "   or:" who (append shared-cli repl-cli))]
    [(all help)
     (when ht (hashtable-delete! ht "help"))
     (display-options shared-cli ht)
     (printf "\n  Testing options:\n")
     (display-options testing-cli ht)
     (printf "\n  REPL options:\n")
     (display-options repl-cli ht)]
    [(all details)
     (wrap 0
       "By default, " who " runs all tests found by searching for *.ms files"
       "in the current directory and its subdirectories. To run a limited set"
       "of tests, specify a suite or a suite and test or use --tag or --not to"
       "filter tests based on tags.\n"
       "\n"
       "If --report <file> is specified, " who " writes an HTML report to that file."
       "If --coverage <file> is specified, " who " writes an HTML coverage report"
       "to that file."
       "The glob arguments to --include and --exclude should be quoted to prevent"
       "expansion by the shell. Within a glob pattern, ** and * differ in that the"
       "former will match path-separator characters while the latter will not."
       "\n")]
    [(all examples)
     (printf "Examples:\n")
     (newline)
     (example (who "--progress suite --report result.html .")
       "Run all tests found in the current directory and its subdirectories."
       "Echo suite-level progress output to the console and write detailed results"
       "to result.html.\n")
     (example (who "--save-profile prof.dat src")
       "Save profile data to prof.dat while running all tests found"
       "under the src directory, overwriting prof.dat if it exists.\n")
     (example (who "--load-profile p1.dat p2.dat --save-profile both.prof")
       "Load profile data from p1.dat and p2.dat and save to both.prof.\n")
     (example (who "-s src --load-profile p1 p2 --coverage ./cov/summary.html")
       "Load profile data from p1 and p2 and generate a coverage report"
       "in ./cov/summary.html,adding \"src\" to the set of directories"
       "searched for source files referenced in the profile.\n")
     (example (who "src/foo.ms")
       "run all tests in ./src/foo.ms\n")
     (example (who "src/foo.ms t1 t2 t3 src/bar.ms")
       "run tests t1, t2, and t3 from ./src/foo.ms and all tests from src/bar.ms\n")
     (example (who " --tag fast --not regression src web")
       "run only the tests under src and web whose mat tags include fast,"
       "but not regression.")])
  (let ([invalid (fold-right remq sections help-sections)])
    (unless (null? invalid)
      (fail "unrecognized help sectionp:狺戾铉翳轭鲠扉洎轭鲠扉洎┅ㄥ轸癌ㄤ彐轭疳蝮瀛扉怛狎扉猢疳蜥礤翦蜷ㄛ扉怛狎溟蝈泗矧殄扉廨扉怛狎溟蝈泗矧殄螬┅ㄤ彐轭ㄦ轭洵骈戾疳翳屮翦铙轱铙ㄤ彐轭ㄣ镯忾铄疳翳骖ㄩㄥ聃犰疳翳骖疳翳泔礅轭疳翳骖┅戾箦狎汨ㄛ疳翳疳翳坭轸Ж┹磲翥ㄣ狒汨扉篝溟蝈泗矧疳翳┅郏ㄅ厣蝈狍镱栝趔郜骘躅ㄦ镬洵戾骠灬礅溽ㄨ轸孱趄磲翥孱趄郇骖滥梢盼赃纳药箦狎汨ㄣ镯忾铄疳翳骖栝趔┹郇骖滥梢盼赃粕膛ㄩ礤礅弪疳翳屮翦铙轱骖屮翦铙轱铙ㄣ镱ㄣ镯忾铄疳翳骖栝趔栝趔┹郜栝趔荸换铒骘祆秣轭簌盱轭塍栝趔骘躅洎荸┅ㄩ眇矧篦轶磲舂篦轶痱镦殪濠篦轶翦篝轭绌ㄤ彐轭ㄨ繇飙蝈痫螋骈戾钺礤蝈痫螋骈戾螬ㄤ彐轭磲舡溽翎娟箫磲舡溽翎牦镱喉犭瀛镡赍泗垓屦矧舡骈戾磲舡溽翎蝈痫螋骈戾磲舡溽翎┹垌弭岘溽翎礤翎溽翎娟箫磲舡溽翎礤翎溽翎磲舡溽翎┅垓弩蹯趔蝈篚祠蟓娟箫磲舡溽翎蝈篚祠磲舡溽翎┅荸ㄤ彐轭礤翎溽翎娟箫礤翎溽翎戾ㄛ镡牦镱喉犭瀛镡赍泗┹ㄦ矧遽汨灬礅溽礤翎溽翎戾ㄛ脲礤翎溽翎脲礤翎溽翎┹牦镱哄翦钿镡赍泗镡埕弩舡骈戾礤翎溽翎翦篝骈戾礤翎溽翎┹郜脲痱屦狎瀛礤翎溽翎脲礤翎溽翎鲠祯礤翎溽翎┅荸┅礤翎溽翎镡戛ㄤ彐轭痱屦狎瀛礤翎溽翎脲鲠飑磲翥脲垠镦赭狎瀛轭骘牦镱后趄轭绛撅怅邈鲠飑郜鲠燧┅ㄤ彐轭蝈篚祠蟓娟箫蝈篚祠磲灬礅溽牦镱喉犭瀛镡赍泗埕弩舡骈戾磲舡蝈篚祠翦篝骈戾┹埕弩簌礅镬倔趄轭磲舡蝈篚祠翦篝┅埕徵磲簌礅镬倔趄轭磲舡蝈篚祠翎珞┅埕疱簌礅镬倔趄轭磲舡蝈篚祠豉疱┅垌弩筢珏磲舡蝈篚祠礤篌徵┹垠篝狒戾ㄛ磲舡蝈篚祠篌翎趔┹牦镱喉犭瀛镡赍泗坫瘐糸礤漉蜥糸镱篌翎趔沭螬┹垓遽糸礤漉蜥糸镱篌翎趔蝈犰螬┹垅翦篌翎趔怡翦螬坨悱泔躅篌翎趔玢泔躅螬坨悱沭糸礤漉蜥糸镱篌翎趔玢沭螬┹坨悱蝈犰糸礤漉蜥糸镱篌翎趔玢蝈犰螬┹坨悱怡翦篌翎趔玢怡翦螬荸┹┅蝈篚祠┅ㄤ彐轭糸礤漉蜥糸镱舂ǐ糸礤箦泔钿舂ǒ糸礤钺铒箦泔钿舂卑鞍鞍鞍鞍癌┅ㄤ彐轭篝蜷铉殒ㄩ篝蜷铉ㄦ矧磲螈┅ㄤ彐轭磲脲蝻惚悴愠戾ㄛ蝈篚祠矧愠啜箴犷篝戾泔祜蚝０胺瓢盎┅⑿劣英┅荸啜趄翡惚翡篝蜷铉殒悴┅翡蝈篚祠┅┅ㄤ彐轭磲脲蝈篚祠颟戾ㄛ钺礤磲舡蝈篚祠翦篝颟荸磲翥磲舡蝈篚祠豉疱颟垧狍磲脲蝻钺礤ｆ┹垠腴鲲殇┹坻衢磲脲蝻钺礤磲舡蝈篚祠礤篌徵颟┹┅ㄤ彐轭蝈篚祠蠹ㄤ彐轭ㄧ弭钺礤颟簌礅镬倔趄轭磲舡蝈篚祠翦篝颟┅篝蜷铉汩伎ㄧ弭钺礤ㄧ弭钺礤┅戾ㄛ磲舡溽翎磲祜徜蝈篚祠蝈痫螋骈戾螬埏镳孱骈戾麸蝈痨徙骈戾钺礤┹镱屮轸ㄣ祜箦痫螋镳ㄨ繇飙倔趄轭镳啜梏盱ㄨ遽礤翎ㄣ栳蝮弭⒄云涪┅┅筱蜷痿换箦疳蜥翦筱蜷痿翎麸桢祓狨麸磲翦翦篝疳蝮秕麴豸ㄦ矧磲Ⅵ狎溽翎峄牦镱猴怅邈舡倔趄轭磲磲舡溽翎娟箫磲舡溽翎┅┅筱蜷痿㈡躅泗轱弩汜疱若盱翦舂鲠磲ЕШЕ犴鸹КЪШЕ祠户ЬШЕ玺户кШЕ聃雉户堍к⒑Е０彻户蝈趱蝾翦舢蝈痨徙濞郐季堍л绗骢钽糸镱愆蝈趱蝾磲疔磔┗㈡躅泗轱蝈钿弪湾翎礤翎鲠轭骘礤翎郄箫骠麽蝈轭骘л殒ā轭骘蝈趱蝾Ё鲠脲圯骘脲轭轭骘脲螽瘐箬脲┗脲螽箫螋è岈猢骄猢蝈趱蝾嗉溟沆狍蠼堍篚轸瀛礤翎堍脲螽磲皎脲骄嗉溟沆狍蠼堍痱镤蹉糗⒕俭疳沆狍蠼堍痱镤蹉舡钺礤堍兢弩汜疱若盱ㄩ铈镗脲蒇ю蝻漉泗钺礤л集箴犷俭疳沆狍蠼堍鲥蝮轱钴⒕弩汜疱若盱ㄩ铈镗脲蒇鲥蝮轱瞌荸集箴犷间轹沆狍蠼堍蝈鲩箝镱堍兢弩汜疱若盱ㄩ铈镗脲蒇蝈鲩箝镱л集溟鼍集溟鼍喋觑轭ěЗ集溟鼍嗷㈡躅泗轱蝈钿弪吁轸濞篚轸濠蝈趱蝾嗉溟沆狍蠼堍篚轸遘⒕间轹沆狍蠼堍篚轸瀛钺礤堍弩汜疱若盱篚轸遨堍礤翎溽翎堍蒇堍翦篝骈戾堍荸蝈钿弪湾翎篚轸遨堍礤翎溽翎堍荸集溟鼍间轹沆狍蠼堍篚轸瀛蝈篚祠筌⒕蝈钿弪义篚祠蟥篚轸遨堍蝈篚祠筌⑤集溟鼍集溟鼍嗷Ⅵ狎箬秣郁狒轶糸泱虎Ⅵ狎箬秣领煲弩蹯趔虎㈡躅泗轱蝈钿弪义篚祠蟥蝈篚祠螬蝈篚祠螽箫螋è岈猢骄岙翦篝猱翦篝┗蝈趱蝾蝈篚祠螽骈祠弪骄箬秣领煲弩蹯趔汨邈脲虍豉疱〗ю狍螫磲皎骄嗉溟沆狍蠼堍蝈篚祠堍间轹沆狍蠼堍蝈篚祠轭骘堍俭疳沆狍蠼堍蝈篚祠钺礤堍兢弩汜疱若盱虍翦篝集箴犷俭疳沆狍蠼堍蝈篚祠翎珞堍兢虍翎珞箫螋è岈猢骄猢磲皎弩汜疱若盱┊觑轭ěЗ集箴犷集溟鼍蝈钿弪赠疱虍豉疱蝈钿弪湾篌徵逑蛴翎趔颟集溟鼍喋觑轭ěЗ㈡躅泗轱蝈钿弪湾篌徵逑蛴翎趔颟蝈趱蝾虍礤篌徵濠嗉溟沆狍蠼堍蝈篚祠礤篌徵遘⒕弩汜疱若盱虍礤篌徵濠集溟鼍蝈钿弪芋翎趔虍篌翎趔┗㈡躅泗轱蝈钿弪赠疱豉疱蝈趱蝾嗉溟沆狍蠼堍蝈篚祠豉疱堍兢豉疱集溟鼍嗷㈧弭祜汜戾铄深綮熙礅弪骑蝽狒ī蝈箫祧邃橡糸镱蟥┊祜汜戾虎㈧弭骘蝽狒翦铄深綮熙礅弪骑蝽狒祜汜戾篝戾т邈轫犰К黹铋眭砥蜥泗轱钅殓轸蠛超磲轫蹴乞徙糸镱拈玳趔┗㈡躅泗轱蝈钿弪孽蜥糸镱ㄤ躜狒轱瞟蝈趱蝾豉疱镦ㄤ躜狒轱瞟浇ь蹴忮颛骘蝽狒翦虍骘蝽狒ㄤ躜狒轱瞟漉蜥糸镱㈡躅泗轱蝈钿弪芋翎趔篝狒螬蝈趱蝾箬秣郁狒轶糸泱汨邈脲洎嗉溟沆狍蠼堍蝈篚祠篌翎趔堍间轹沆狍蠼堍蝈篚祠篌翎趔沭踯⒕蝈钿弪孽蜥糸镱篝狒螽沭酴集溟鼍间轹沆狍蠼堍蝈篚祠篌翎趔蝈犰堍兢蝈钿弪孽蜥糸镱篝狒螽蝈犰集溟鼍间轹沆狍蠼堍蝈篚祠篌翎趔怡翦筌⒕篝狒螽怡翦簖集溟鼍间轹沆狍蠼堍蝈篚祠篌翎趔玢泔躅糗⒕篝狒筵堍玢泔躅糗⑤集溟鼍间轹沆狍蠼堍蝈篚祠篌翎趔玢沭踯⒕蝈钿弪孽蜥糸镱篝狒筵堍玢沭踯⑤集溟鼍间轹沆狍蠼堍蝈篚祠篌翎趔玢蝈犰堍兢蝈钿弪孽蜥糸镱篝狒筵堍玢蝈犰堍荸集溟鼍间轹沆狍蠼堍蝈篚祠篌翎趔玢怡翦筌⒕篝狒筵堍玢怡翦筌⑤集溟鼍集溟鼍Ё㈡躅泗轱蝈钿弪吁眄狎ㄤ狒岍鲠篚眄狎疳篌艾箅轲艾驷殪溽翎骘蚺徙瑷篚轸骄篚轸瀹蝈篚祠螽骘蚺徙瑷骄篚眄狎垓豉疱暴┗戾箅轲疱篚眄狎箅轲箅轲疱篚眄狎箅轲┼驷祗寤殒篚眄狎驷殪蝈趱蝾嗉溟沆狍蠼堍篚眄狎驷殪堍酒衢戾篚眄狎驷殪镦篚眄狎驷殪篚眄狎疳篌翦篝螭箅轲疱潼Ё集溟鼍嗷殒箅轲疱洎蝈趱蝾嗉溟沆狍蠼堍篚眄狎疳篌堍攫狍箦篚眄狎疳篌翦篝螭箅轲疱潺集溟鼍嗷蝈趱蝾嗉溟沆狍蠼堍篚眄狎疳篌堍拘劣优犰篚眄狎疳篌翦篝螽集溟鼍嗷ㄦ矧磲㈧弭桢徜弪峄牦镱猴怅邈舡倔趄轭牦镱喉犭瀛镡赍泗垌弭岘溽翎牦镱喉犭瀛镡赍泗埕弩舡骈戾⒂蹰翦⑤┹垓弩蹯趔扉篝牦镱喉犭瀛镡赍泗埕弩⑽犴澧埕徵Ж┹埕疱⒁弩蹯簪垠篝狒牦镱喉犭瀛镡赍泗坫瘐沭酡垓遽Ⅱ遽膦垅翦⑩翦螈坨悱泔躅敲⑤坨悱沭⑶沭酡坨悱蝈犰⑶蝈犰⑤坨悱怡翦⑶怡翦螈荸荸┹┅㈡躅泗轱蝈钿弪ㄤ狒岍戾篚眄狎滹沲礤铘珏襞戾礤铘蛮射ě篚眄狎З戾泔铘孱滹沲礤铘珏襞戾礤铘蛮射ě泔铘孱臾┗篚眄狎轭铄蛉酝蝈钿弪吁眄狎ㄤ狒岍泔铘孱舢轭铄蛉酝蝈钿弪吁轸濞桢徜弪螬溽翎磲皎蝈钿弪吁轸濠觑轭ěЗ滹沲礤铘徜渑鲥铘涕篝孱弪ě南兔镱翦铘田徜邃К骢钽糸镱ㄥ鲥铘箬秣郁狒轶糸泱滹沲礤铘珏襞戾礤铘蛮射ě箬秣郁狒轶糸泱З箬秣郁狒轶糸泱汨邈脲驷祗寤箬秣郁狒轶糸泱镱沆殂弼孱骄蝈钿弪ㄤ狒岍箬秣领煲弩蹯趔滹沲礤铘珏襞戾礤铘蛮射ě箬秣领煲弩蹯趔З箬秣领煲弩蹯趔汨邈脲溽翎弼弪礓骄礓蝈篚祠螽弼弪骄ю狍螫浇虍豉疱箅轲浇虍豉疱┅箬秣领煲弩蹯趔镱沆殂弼孱骄蝈钿弪ㄤ狒岍蝈钿弪ㄤ狒岍┗篝戾桢徜弪忉汶珧秕钿扉玷翮蜥磲蜱轭怙趑镯靛ｓ蹴磲蝙骘铘箝搴灬蜱寤篚眄狎驷殪泔祜蚝蝈浠篚眄狎疳篌ｃ镱趄镬溟箴灬骒屮犰殓瞽轸屙蠛沐铘弪ｓ栾饔翎糸篝殂磲蜱轭戾骠卞砘篚轸溟箴灬骒屮骒屮溟蝈泗轱詈蝻骰犰殓瞽轸屙蠛骈蝮忉箦扉铄怙蜾弪怙趑镯泔祜蚝扉玷翮蝈怙蜾弪怙趑镯篝戾箫扉浠怙蜾弪怙趑镯鏖漪韬别篚轸瀛钺礤鏖漪韬舶鲼痫箝糸镱蝈灬糸鲥篚轸瀛钺礤篚轸瀛礤翎溟箴灬铒铄鲩箝忾扉豉栝滗孱镳徙轸盎篚轸瀛钺礤鸿秭弪篚轸瀛礤翎痫箝糸镱徕箫祯翦轭溴被溟箴灬忪镢牖鲩箝忾扉豉鲩箝忪寤镳徙轸被疳滗轭绾卑瘌鏖漪韬嘲梆忉汶珧秕钿蜱忉ú窗泊艾泊艾暴怙蜾弪蜥溟躞卑瘌篚轸瀛蝈篚祠蝈篚祠溟箴灬骒屮骒屮溟蝈泗轱詈蝻骰蝈篚祠轭骘鏖漪韬舶鲼蝈篚祠钺礤蝈篚祠翎珞骘铘驷黹禊筢铙骘铘箝搴箜犰旎磲蜱轭戾骠卞砘蝈篚祠疳篌鏖漪韬村砘翦舡趄犷箧矧砗躔疱蜚狍寤泔祜蚝珧邋罨蝈篚祠箅轲鏖漪韬村砘翦舡趄犷箧矧砗躔疱蜚狍寤骘铘篝戾镡扉聃寤蝈篚祠驷殪鏖漪韬村砘翦舡趄犷箧矧砗躔疱蜚狍寤泔祜蚝蝈浠蝈篚祠礤篌徵鏖漪韬蛋鲼蝈篚祠义篚祠鏖漪韬村砘蝈篚祠篌翎趔溟箴灬骒屮翦舡犰殓詈蜷玷艋蝈篚祠篌翎趔沭鏖漪韬跺砘蝈篚祠篌翎趔蝈犰鏖漪韬跺砘蝈篚祠篌翎趔怡翦鏖漪韬峰砘蝈篚祠篌翎趔玢泔躅鏖漪韬村砘蝈篚祠篌翎趔玢沭鏖漪韬跺砘蝈篚祠篌翎趔玢蝈犰鏖漪韬跺砘蝈篚祠篌翎趔玢怡翦鏖漪韬峰砘ㄢ镤篝戾㈡镱舡驷黹禊喉镱矬疳沐虎┅ㄤ轹ㄣ灬篌㈣遽溴颌┅ㄤ轹ㄩⅢ蹴磲蝙┅ㄤ轹ㄩ泔铘蝻祗┅ㄩ铕豸豉疱汨邈脞秫ㄩⅢ栾髁祆义篚祠螈┅灬忮ㄦ矧Ⅲ栾髁祆义篚祠螈┅⒂栾领膦ㄩ铕豸豉疱汨邈脞秫ㄩⅢ栾饔翎糸篝殂螈┅灬忮ㄦ矧Ⅲ栾饔翎糸篝殂螈┅⒂栾郁狒轶糸泱┅ㄤ轹ㄩ泔铘孱簪┅┅┅┅ㄤ彐轭ㄣ镱箫戾篚眄狎蝈痫螋骈戾螬戾舡鲠祯弩ㄛ疳篌驷殪箅轲篚眄狎辁蝈痫螋骈戾螬荸痱轭翩⒃弩趔蝓詈嗅篌漆殪与轲筌钴睥ǐ疳篌驷殪疳篌驷殪箅轲┅ㄤ彐轭ㄩ姝屮轶趔骖ㄡ钿蝈珲灬颦骈戾骖骖┅ㄤ彐轭ㄧ弭翦篝骈戾ㄡ钿ㄤ轵邈麸蝙ㄦ轭洵骈戾㈨螈Ⅲ螈┅ㄤ彐轭ㄧ弭翦篝篚轸矧ㄩ姝屮轶趔ㄩ姝屮轶趔篝蜷铉狃疱钿眢┅┅ㄤ彐轭疳翳炬殪瀛躜疳翳ㄤ彐轭筢铋糸疳翳觑轭磲梏麴吼弪沐铘孱泔溴痱彗屮瓠箴扉③苘苘茛疳翳┅┅磲翥痱彗屮瓠磲翥⑥ㄛ岘镰谳憨圮苘墀莰疳翳郇犷汨矧蝈篝篝蜷铉狃疱钿㈡殪搴犷汨矧筢铋糸蝈篝┅郏篝蜷铉狃疱钿㈡殪搴筢铋糸疳翳┅荸ㄤ彐轭ō掘屦矧舡骈戾钺礤骈戾钺礤篝蜷铉狃疱钿骈戾钺礤盹┅ㄤ彐轭瀛簌铘狲骘蝈徙簌铘狲蝓戾ī郇ㄛ鲠祗怙澌怙澌ㄦ矧遽汨灬礅溽鲠怙澌怙澌祗┹┅ㄤ彐轭ㄥ趄徙舡篚轸弩箴邈螬戾ㄛ梏磲脲栳箬翎忪篝蜷铉栳箬篝蜷铉娇┹ㄤ彐轭ㄡ滗篚轸篚轸濠ㄨ狍梏徕戾躔溽翦梏篚轸灬礅溽镬洎矧镬磲脲栳箬翎忪篝蜷铉栳箬篝蜷铉娇┅ｆ┅戾祓ㄛ箴邈箴邈筝ㄣ镱郇铛祆箴邈螬戾舡鲠祯弩ㄛ脲鲠祗ㄨ狍梏徕戾孱趄殄梏┹鲥泗矧眷轶鲥泗矧磲灬礅溽篚轸翦篝箦舂ㄣ镱篚轸磲篝蜷铉倔礅镬鲥泗矧眷轶ㄨ狍梏徕戾脲翦篝箦舂┅┅脲鲠祗┅┹郇珏舡翦篝骈戾ㄣ狎箴邈螬骄灬礅溽ㄦ殪弩ㄦ矧遽汨徜洵篚轸骈戾螬祓ㄣ潋箴邈螬┅郇珏舡翦篝篚轸ㄣ狎箴邈螬骄灬礅溽篚轸濠ㄡ滗篚轸篚轸濠戾ㄛ趑ㄨ狍梏徕戾蝈梏篚轸ｆ┹ㄤ彐轭ㄡ滗翦篝翦篝ㄨ狍梏徕戾箦簟趑翦篝ｔ┅戾祓ㄛ祗ㄣ潋箴邈螬荸磲翥祗郇祓祗┹郇簪ㄦ衢镳糸镱屮疱泗鲠祯搴簪┹郇簪翦篝蝈篝ㄡ滗翦篝翦篝祓蝈篝┹郇翦篝蝈篝ㄧ踽蜾铒矧ㄤ轵邈麸蝙翦篝ㄧ弭翦篝篚轸翦篝┅┅ㄡ滗翦篝翦篝祓蝈篝┹郜蝈篝祓蝈篝┹┅┅坼祗ㄦ衢㈠疱泗邃溟蝈泗矧矧篚轸搴螈ㄣ狎箴邈螬┹┅┅ㄤ彐轭镳糸镱钺礤钺礤ㄦ矧磲舡箴邈矧ㄦ轭灬礅溽ㄥ聃犰钺礤坚蜱箴邈钺礤┅犷沆椹ㄦ衢③轭翦蝾犰铒镳糸镱钺礤螈钺礤┅Ж矧祜铉箬矧舂┅ㄤ彐轭趄疳蝮濠ㄤ彐轭痱镡戾ｆㄤ彐轭蝈痨镳疳蝮瀛泔眄犷洵扉铄狎珲礤铘ㄡ痧孱箬狎邃沆黹筱沆蝈痨沆椹ㄣ镯磲钿扉铄狎珲礤铘螬灬礅溽躅戾篌痱镡戾箦簟痱镡戾┅┅ㄩ蝈痨镳Ⅱ屦膦ㄩ痱镡戾ㄡ痧禊驷殪痱镡戾愆蝈痨镳舂疳蝮瀛泔眄犷洵扉铄狎珲礤铘ㄡ痧孱箬狎邃沆翦篝轭绛沆椹┅ㄤ彐轭镳趄疳蝮濠ㄣ镱郇镳Ⅲ蜚溟蝮骄灬礅溽篁沅轵螬箫躜沐溟蝈泗矧殄ㄡ痧孱篁沅轵箫躜沐溟蝈泗矧殄螬┅┹扉怛狎溟蝈泗矧殄ㄦ镬洵蜷玷灬礅溽徙悌ㄡ痧孱疳蝮瀛扉怛狎徙悌扉怛狎溟蝈泗矧殄螬矧镳㈧殁溟蝮Ж┅┅ㄣ镱郇弪锟ㄨ狍梏徕戾箝镳舂┅躞徵ｔЖｆ┹郇镳㈣屐稷骄灬礅溽箦泗轱铙躞徵ｆ磲篝蜷铉倔礅镬箦泗轱铙镳舂┅郇镳Ⅵ弪箝镱痱轭翩皱蝮轱岍茴麒箫骠麽蝈鲥蝮轱篦轶瑭箫骠麽蝈蝈鲩箝镱篦轶瑭┹郇栳箬翎忪瀛蝈镳舂Ⅱ屦膦ｆ戾ㄛ殓铒蝈蝈眈ｆ磲灬礅溽ㄡ钿镳坚蜱箴邈钺礤┅ㄦ矧磲舡箴邈Ж矧祜铉箬矧舂┅黹筱沆椹┹麒孱疳轵殓铒蝈洎痱轭翩累社铒蜷铉狺蕃 in a mode\n]" ignored
        (option-named "repl"))))
  (apply (scheme-start) (cons "--" (or (opt "files") '())))]
 [else
  (let ([incl-tags (map string->symbol (or (opt "incl-tags") '()))]
        [excl-tags (map string->symbol (or (opt "excl-tags") '()))]
        [exclude (or (opt "exclude") '())]
        [include (or (opt "include") '())]
        [save-prof (opt "save-prof")]
        [load-prof (opt "load-prof")]
        [progress (string->symbol (or (opt "progress") "test"))]
        [report (opt "report")]
        [coverage (opt "coverage")]
        [suites (sort (lambda (s1 s2) (string<? (car s1) (car s2)))
                  (extract-suites (or (opt "specs") '())))])
    (cond
     [(and load-prof (not (or save-prof coverage)))
      (fail "a requires a or a"
        (option-named "load-prof")
        (option-named "save-prof")
        (option-named "coverage"))]
     [(and coverage (not (or load-prof save-prof)))
      (fail "a requires a or a"
        (option-named "coverage")
        (option-named "load-prof")
        (option-named "save-prof"))])
    (let ([test-files (map car suites)])
      (define to-file (and (or report coverage) #t))
      (define report-files (map ->report-filename test-files))
      (define status 0)
      (define no-tests '())
      (define full-load-filenames
        (map
         (lambda (load-fn)
           (match (catch (get-real-path load-fn))
             [#(EXIT ,reason)
              (fail "a a: file not found" (option-named "load-prof") load-fn)]
             [,full-load-fn full-load-fn]))
         (or load-prof '())))
      (define (truncate-profile)
        (match (catch (open-file-to-replace save-prof))
          [#(EXIT ,reason)
           (fail "a a failed (a)" (option-named "save-prof") save-prof
             (exit-reason->english reason))]
          [,op (close-port op)]))
      (cond
       [(and load-prof save-prof)
        (let ([add-to-profile
               (match (catch (get-real-path save-prof))
                 [#(EXIT ,reason) #f]
                 [,full-save-fn
                  (and (member full-save-fn full-load-filenames) full-save-fn)])])
          (unless add-to-profile (truncate-profile))
          ;; mute the console-event-handler
          (parameterize ([console-error-port (open-output-string)])
            (match-let* ([#(ok ,pid) (profile:start add-to-profile save-prof #t)])
              (foreach ([to-load (map cons full-load-filenames load-prof)])
                (match-let* ([(,full-name . ,user-supplied-name) to-load])
                  (unless (equal? full-name add-to-profile)
                    (match (catch (profile:merge full-name))
                      [ok (void)]
                      [#(EXIT ,reason)
                       (fail "cannot load profile data from a" user-supplied-name)]))))
              (profile:stop))))]
       [save-prof (truncate-profile)])
      (when to-file
        (foreach ([file report-files])
          (catch (remove-file file))))
      (foreach ([suite/test suites] [report-file report-files])
        (let ([suite (car suite/test)]
              [tests (cdr suite/test)])
          (match (run-test-spec swish
                   (<test-spec> make
                     [test-file suite]
                     [report-file (and to-file report-file)]
                     [tests (and (pair? tests) tests)]
                     [incl-tags incl-tags]
                     [excl-tags excl-tags]
                     [profile save-prof]
                     [progress progress]
                     [lib-dirs (library-directories)]
                     [src-dirs (source-directories)]))
            [#f (void)]
            [fail (set! status 1)]
            [skip (set! status 1)]
            [no-tests (set! no-tests (cons suite no-tests))])))
      (let ([report-files (filter file-exists? report-files)])
        (when report
          (html-report report report-files)
          (printf "see a\n" (path->file-uri (get-real-path report))))
        (when coverage
          (if (profile:dump-html (or save-prof load-prof) coverage include exclude)
              (printf "see a\n" (path->file-uri (get-real-path coverage)))
              (printf "no profile to dump\n")))
        (when (or report coverage)
          (console-summary report-files))
        (match (filter (lambda (fn) (string=? "ms" (path-extension fn))) no-tests)
          [() (exit status)]
          [,expected-tests
           (unless (eq? progress 'none)
             (printf "No tests found in茴狺茴屮疱泗邃翦篝螬ㄥ轸ㄩ篝狒躞癌篝狒躞畅┹┅┅荸