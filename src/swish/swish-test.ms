;;; Copyright 2018 Beckman Coulter, Inc.
;;;
;;; Permission is hereby granted, free of charge, to any person
;;; obtaining a copy of this software and associated documentation
;;; files (the "Software"), to deal in the Software without
;;; restriction, including without limitation the rights to use, copy,
;;; modify, merge, publish, distribute, sublicense, and/or sell copies
;;; of the Software, and to permit persons to whom the Software is
;;; furnished to do so, subject to the following conditions:
;;;
;;; The above copyright notice and this permission notice shall be
;;; included in all copies or substantial portions of the Software.
;;;
;;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
;;; EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
;;; MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
;;; NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
;;; HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
;;; WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
;;; OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;;; DEALINGS IN THE SOFTWARE.

(import
 (chezscheme)
 (swish mat)
 (swish profile)
 (swish script-testing)
 (swish testing)
 )

(define swish-test-src (path-combine "src" "swish" "swish-test"))

(define (swish-test . args)
  (let ([os (open-output-string)])
    (for-each (lambda (arg) (display arg os)) args)
    ;; not using filenames that contain spaces
    (remove "" (split (get-output-string os) #\space))))

(define (zero-exit args expected)
  (script-test swish-test-src args "" expected))

(define (nonzero-exit args expected-stdout . expected-stderr)
  (match-let*
   ([#(EXIT `(<os-process-failed> ,stdout ,stderr ,exit-status))
     (catch (zero-exit args '()))]
    ;; we get a non-zero exit code if any test fails
    ;; or if we skip tests that were otherwise selected
    [#f (= exit-status 0)]
    [,_ (match-regexps expected-stdout stdout)]
    [,_ (match-regexps expected-stderr stderr)])
   'ok))

(define (path-relative path)
  (substring path (+ 1 (string-length (cd))) (string-length path)))

(isolate-mat installation ()
  ;; We use the relative path to make the coverage report tidier.
  ;; This test makes sure we're still testing what we installed.
  (assert
   (equal?
    (read-file (path-combine (prereq-path) "swish-test"))
    (read-file swish-test-src))))

(isolate-mat help ()
  (zero-exit (swish-test)
    '("Usage:"))
  (zero-exit (swish-test "--help")
    '("Usage:"
      seek
      "progress <mode>"
      seek
      "<spec> .* directory .* suite"))
  (zero-exit (swish-test "-h all")
    '("Usage:"
      seek
      "progress <mode>"
      seek
      "<spec> .* directory .* suite"
      seek
      "By default,"
      seek
      "Examples:"))
  (nonzero-exit (swish-test "--help me-obi-wan")
    '()
    "swish-test: unrecognized help section: me-obi-wan")
  )

(isolate-mat version ()
  (zero-exit (swish-test "--version")
   '("swish-test Version (?:\\d+\\.){2}\\d+ \\([[:xdigit:]dirty-]+\\)")))

(isolate-mat check-options ()
  (nonzero-exit (swish-test "--coverage foo")
    '() "--coverage requires --load-profile or --save-profile")
  ;; check for required options before complaining about missing file
  (nonzero-exit (swish-test "--load-profile file-that-does-not-exist")
    '() "--load-profile requires --save-profile or --coverage")
  (nonzero-exit (swish-test "--report foo.html")
    '()
    (pregexp-quote
     "--report requires --harvest or --load-results \
      or a set of tests to run (<file> ...)"))
  (nonzero-exit (swish-test "--harvest . --progress none .")
    '() "--harvest conflicts with <file> ...")
  (nonzero-exit (swish-test "--load-results foo.json .")
    '() "--load-results conflicts with <file> ..."))

(isolate-mat load-this ()
  (write-test-file "test-load-this.ss"
    (lambda ()
      (display "#!chezscheme\n")
      (for-each pretty-print
        '((define a 1)
          (define-syntax b (identifier-syntax 2))))
      (display "#!eof mats\n")
      (for-each pretty-print
        '((import (swish mat) (swish testing))
          (load-this)
          (mat t1 () (assert (= a 1)))
          (mat t2 () (assert (= b 2)))))))
  (script-test swish-test-src
    `("--progress" "test"
      ,(path-combine (output-dir) "test-load-this.ss"))
    ""
    '(seek "Tests run: 2   Pass: 2   Fail: 0   Skip: 0")))

(isolate-mat load-this-exposing ()
  (write-test-file "test-exposing.ss"
    ;; - explicitly exported variable
    ;; - exposed variable
    ;; - exposed macro
    ;; - no access to un-exported, un-exposed variable
    (lambda ()
      (display "#!chezscheme\n")
      (pretty-print
       '(library (test-exposing)
          (export a)
          (import (scheme))
          (define a 123)
          (define b 404)
          (define-syntax c (identifier-syntax 9000))
          (define d 'enied)
          (define-syntax e (identifier-syntax "vaded"))
          "non-definition form"))
      (display "#!eof mats\n")
      (for-each pretty-print
        '((import (swish mat) (swish testing))
          (load-this-exposing '(b c))
          (import (test-exposing))
          (mat exported () (assert (= a 123)))
          (mat exposed-variable () (assert (= b 404)))
          (mat exposed-macro () (assert (= c 9000)))
          (mat invisible-variable ()
            (match-let*
             ([#(EXIT ,reason) (catch d)]
              ["Exception: variable d is not bound." (exit-reason->english reason)])
             'ok))
          (mat invisible-macro ()
            (match-let*
             ([#(EXIT ,reason) (catch e)]
              ["Exception: variable e is not bound." (exit-reason->english reason)])
             'ok))))))
  (write-test-file "test-reexpose.ss"
    ;; - load-this-exposing adds export for variable already exported
    (lambda ()
      (display "#!chezscheme\n")
      (pretty-print
       '(library (test-exposing)
          (export a)
          (import (scheme))
          (define a 123)
          (define b 404)
          "non-definition form"))
      (display "#!eof mats\n")
      (for-each pretty-print
        '((import (swish mat) (swish testing))
          ;; re-expose a, which is already exported
          (load-this-exposing '(a b))
          (import (test-exposing))
          (mat exported () (= a 123))
          (mat exposed-variable () (= b 404))))))
  (script-test swish-test-src
    `("--progress" "test"
      ,(path-combine (output-dir) "test-exposing.ss")
      ,(path-combine (output-dir) "test-reexpose.ss"))
    ""
    '(seek
      "Tests run: 5   Pass: 5   Fail: 0   Skip: 0"
      seek
      "Tests run: 2   Pass: 2   Fail: 0   Skip: 0")))

(isolate-mat swish-test-repl ()
  (define tmp-prof (path-combine (output-dir) "test-repl.prof"))
  (write-test-file "test-repl.ss"
    (lambda ()
      (for-each pretty-print
        '((define (loaded? L) (and (member L (library-list)) #t))
          (assert (andmap loaded? '((swish mat) (swish profile) (swish testing))))
          (printf "okay\n")))))
  (with-output-to-file tmp-prof void 'replace)
  (script-test swish-test-src `("--repl" ,(path-combine (output-dir) "test-repl.ss"))
    (format "筌铪Ж痱镦殪搴篝狎繇瓠痱镦繇瓠痱镦ｔㄦ矧遽汨痱弭豉痱轭箫螋篝蜷铉伎磲簌礅镬倔趄轭扉怛狎屮痫螋Ж篦轶磲舂┅┅痱镦殪搴筢鲥ㄥ轸┅啜ㄦ矧磲皱蝮轱幄箫骠麽蝈痱镤蹉舡钺礤篦轶瑭箫骠麽蝈鲥蝮轱篦轶瑭镫狴箦咫⑨滗磲簪㈨狒箦咫Ⅱ躅磲趔箦咫Ⅲ蹴磲蜷澧┅筱蜷痿翦篝篦轶璀翦篝篁啜蝈痨祜徜痱镦殪澧㈡镲⑩狎筢鲥痱镦殪澧汨秣溴颌螈㈧蹼澧痱镧蝈篌㈤镱翎纰⒁粕蘑铒簪Ⅱ遽祆ㄦ矧磲筌铪啜痱镦殪搴篝狎繇瓠痱镦繇瓠痱镦ｔ痱弭豉痱轭箫躜沐溟蝈泗矧殄螬痱镦殪搴筢鲥ㄥ轸┅啜箦咫⑸珙矧轭翎绗铒衄痱镧蝈篌祜徜痱镦殪瀣筢鲥痱镦殪轭蝈痨盹溴箦咫ㄜ㈧蹼遘堍苘堍┅麒孱麒弪彘ю蝻骈戾颟痱镦殪搴礤蜱繇瓠痱镦┅ㄩ箫灬翦磲箦戾泗翦篝īㄤ彐轭翦篝骈戾黩轸瀛翦篝骈戾Ⅳ弩舡箴邈殒殄洚眢灬礅溽īㄦ矧遽汨痱弭豉痱轭Жㄩ眇矧篦轶磲舂磲岜趄轹ㄡ篌弪ｔ┅磲岵趄轹ㄡ篌弪ｔ┅磲岢ㄢ蝻脲瞟ㄡ篌弪ｆ┅磲岽īㄡ篌弪矧ｆｔ┅磲岬箝祆ㄡ篌弪㈤镱┅┅┅换蝓孱糸蝈篚轸瀣弭箅轲犰翦篝铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌翦篝翎蜗闻翦篝骈戾Ж箦咫⒂松岜⒂松岵⒂松岢⒂松岽⒂松岬③莴⒃弩趔蝓詈嗅篌漆殪与轲耽┅换蝓翦篝箦戾泗邃怡钺礤弭箅轲犰翦篝铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌翦篝翎蜗闻翦篝骈戾岜岬Ж箦咫⒂松岜⒂松岬③莴⒃弩趔蝓詈嗅篌漆殪与轲并┅换蝓翦篝箦戾泗邃怡翎绗犰翦篝蝓疳篌铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌翦篝翎趄轹翦篝骈戾Ж箦咫疳篌岜疳篌岵⒂松岢⒂松岽⒂松岬③莴⒃弩趔蝓詈嗅篌漆殪与轲尝┅换蝓翦篝屮沆蹁轭怡翎铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌翦篝铒怛镫孱翦篝骈戾Ж箦咫疳篌岜疳篌岵⒂松岢疳篌岽疳篌岬③莴⒃弩趔蝓詈嗅篌漆殪与轲雹┅换翎珞狎徜溟糸鲥铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌翦篝翎趄轹翎怛镫孱翦篝骈戾Ж箦咫疳篌岜疳篌岵⑵辽岢⒂松岽⒂松岬③莴⒃弩趔蝓詈嗅篌漆殪与轲并┅换屮沆躞轱翎珞狎徜溟糸鲥铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌翦篝铒趄轹铒箝祆翦篝骈戾Ж箦咫⒂松岜⒂松岵⑵辽岢疳篌岽⒂松岬③莴⒃弩趔蝓詈嗅篌漆殪与轲尝┅换骈祠弪骈蝮怡翎珞犷翳孱怡屮沆躞轱翎珞换矧溴镱泔眄犷洵扉铄滹弩瞌磲趑弪铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌翦篝翎箝祆铒怛镫孱翎趄轹翎怛镫孱翎趄轹翦篝骈戾Ж箦咫疳篌岜疳篌岵⒂松岢换箝钽铒怛镫孱⒂松岽换箝钽轸滹弩瞌磲翥犷轭沆蹁邃翎珞疳篌岬③莴⒃弩趔蝓詈嗅篌漆殪与轲并┅换殒犷翦篝驷殪蟋篚轸篚眄狎箬秣驷殪铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌篚轸翎怛镫孱翎箝祆翦篝骈戾扉篝ㄦ矧磲驷殪痱彗屮瓠聃雉翦篝骈戾┅┅换殒铒翦篝驷殪蟋篚轸篚眄狎箬秣箅轲疱铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌篚轸翎趄轹翎箝祆翦篝骈戾扉篝ㄦ矧磲疳篌苘箅轲疱曹堠痱彗屮瓠聃雉翦篝骈戾┅┅换驷殪躜屮轸泔溴殒箴邈殒殄翦篝铒骘躅铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌翦篝翦篝骈戾翳轶翦篝滹弩铒舡屮轶簪Ж箦咫⑴沐痿轱轭蝓瞽磲艉磲翳轶翦篝滹弩铒舡屮轶轶铒溴骈铄洧┅换殒犰箴邈殒殄洫翦篝疳篌箬秣疳篌屮轸泔溴弪弪锃屮轸篦轶璀翦篝痱镧蝈篌篚轸翦篝骈戾岜岵岽扉篝ㄦ矧磲疳篌あ痱彗屮瓠聃雉翦篝骈戾┅┅ㄩ箫灬翦磲翦篝溟īㄤ彐轭翦篝溟虬Ⅲ踱溟虬ㄤ彐轭翦篝溟虮疳翳泔礅轭翦篝溟虬Ⅲ踱溟虮┅ㄤ彐轭翦篝溟虿疳翳泔礅轭翦篝溟虬Ⅲ踱溟虿┅ㄤ彐轭骈戾黩轸瀛翦篝骈戾疳翳泔礅轭翦篝溟虮㈡殪灞眢灬礅溽īㄦ矧遽汨痱弭豉痱轭Жㄩ眇矧篦轶磲舂磲舯īㄡ篌弪ｔ┅磲舨īㄡ篌弪ｔ┅┅┅ㄤ彐轭骈戾黩轸瀛翦篝骈戾疳翳泔礅轭翦篝溟虮㈡殪宀眢灬礅溽īㄦ矧遽汨痱弭豉痱轭Жㄩ眇矧篦轶磲舂磲岜īㄡ篌弪ｔ┅磲岵īㄡ篌弪ｔ┅┅┅ㄤ彐轭骈戾黩轸瀛翦篝骈戾疳翳泔礅轭翦篝溟虿㈡殪宄眢灬礅溽īㄦ矧遽汨痱弭豉痱轭Жㄩ眇矧篦轶磲舂磲岜īㄡ篌弪ｔ┅磲岵īㄡ篌弪ｔ┅磲忉īㄡ篌弪ｆ┅┅┅铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌翦篝疳翳泔礅轭秕麴豸溟颟翦篝溟虬┅扉篝痱彗屮瓠聃雉骈戾暴③莴义篚祠藻篝钺礤湾篌徵③莴疳篌舯疳篌舨箦咫痱彗屮瓠聃雉骈戾博③莴义篚祠藻篝钺礤湾篌徵③莴疳篌岜疳篌岵箦咫痱彗屮瓠聃雉骈戾畅③莴义篚祠藻篝钺礤湾篌徵③莴疳篌岜疳篌岵屏商忉洧┅铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌篚轸疳翳泔礅轭秕麴豸溟颟翦篝溟虬┅扉篝ㄦ矧磲疳篌あ痱彗屮瓠聃雉骈戾暴ㄦ矧磲疳篌あ痱彗屮瓠聃雉骈戾博ㄦ矧磲驷殪あ痱彗屮瓠聃雉骈戾畅┅换翦篝栳蝣弩溟蝈泗矧痱镢弩箝铉戾ㄛ泔礅轭邃牦镱疳翳泔礅轭秕麴豸溟颟泔礅轭邃牦镱┹弪锃屮轸篦轶璀翦篝蝈痫螋泔礅轭邃牦镱栳蝣弩疳翳泔礅轭秕麴豸溟颟翦篝溟虬┅Ж⒃弩趔蝓詈嗅篌漆殪与轲阿┅┅ㄩ箫灬翦磲箴邈痱镢弩箝铉īㄤ彐轭泔眄镱钺礤骈戾黩轸瀛翦篝骈戾㈧镲塍扉脲骈戾钺礤眢灬礅溽īㄦ矧遽汨痱弭豉痱轭Жㄩ眇矧篦轶磲舂磲舯īㄡ篌弪ｔ┅磲舨īㄡ篌弪ｆ┅┅┅ㄤ彐轭泔眄镱钺礤篝蜷铉倔礅镬泔眄镱钺礤骈戾┅ㄤ彐轭翦篝骈戾黩轸瀛翦篝骈戾Ⅳ弩舡箴邈痱镢弩箝铉眢灬礅溽īㄦ矧遽汨痱弭豉痱轭啜ㄩ眇矧篦轶磲舂磲泔眄镱钺礤īㄡ篌弪ｔ┅磲岜īㄡ篌弪ｔ┅磲岵īㄡ篌弪ｆ┅磲岢īㄡ篌弪ｆ┅┅┅铒铤弪锃屮轸换泔眄镱钺礤轭翦蝠蝈翦狍篚轸箝钽翳弪轶骈戾镦翳狒钺礤篦轶璀翦篝痱镧蝈篌翦篝翦篝骈戾泔眄镱钺礤扉篝箦咫换篚轸弩狃疱狎轭箫螋邃矧溴痱彗屮瓠聃雉泔眄镱钺礤骈戾③莴义篚祠藻篝钺礤湾篌徵③莴疳篌舯屏商舨③莴⒃弩趔蝓詈嗅篌漆殪与轲阿箦咫痱彗屮瓠聃雉翦篝骈戾③莴义篚祠藻篝钺礤湾篌徵③莴ㄦ矧磲疳篌幄痱彗屮瓠聃雉簌礅镬倔趄轭泔眄镱钺礤┅疳篌岜屏商岵屏商岢③莴⒃弩趔蝓詈嗅篌漆殪与轲阿┅弪锃屮轸换泔眄镱钺礤屮痨殂轸禊殇孱糸骈邃狍翦篝换弪屮轸礤犷蠛换碑麇溟漕蝓泔眄镱钺礤狍篚轸换伯麇蜥镱禊翳翦篝钺礤泔眄镱钺礤篦轶璀翦篝痱镧蝈篌翦篝翦篝骈戾泔眄镱钺礤扉篝箦咫痱彗屮瓠聃雉翦篝骈戾③莴义篚祠藻篝钺礤湾篌徵③莴ㄦ矧磲疳篌幄痱彗屮瓠聃雉簌礅镬倔趄轭泔眄镱钺礤┅③莴⒃弩趔蝓詈嗅篌漆殪与轲阿┅铒铤弪锃屮轸换屮疱泗鲠祯篦轶璀翦篝翦篝骈戾簪ЖⅢ鏖箬翦篝镳糸镱屮疱泗鲠祯搴簪铒铤弪锃屮轸换忉箴邈篦轶璀翦篝滹弩铒舡屮轶舡狍骈戾矧溟蝈泗矧ЖⅢ鏖箬翦篝屮疱泗邃溟蝈泗矧矧篚轸搴堍滹弩铒舡屮轶舡狍骈戾矧溟蝈泗矧堍ㄩ箫灬翦磲蝈痫螋蝈篚祠īㄤ彐轭蝈痫螋骈戾疳翳泔礅轭秕麴豸溟颟Ⅱ屦矧舢梏盱┅ㄤ彐轭蝈痫螋骈戾疳翳泔礅轭秕麴豸溟颟Ⅱ屦矧舯梏盱┅ㄤ彐轭蝈痫螋骈戾疳翳泔礅轭秕麴豸溟颟Ⅱ屦矧舨梏盱┅ㄤ彐轭蝈痫螋骈戾疳翳泔礅轭秕麴豸溟颟Ⅱ屦矧舫梏盱┅ㄤ彐轭蝈痫螋骈戾疳翳泔礅轭秕麴豸溟颟Ⅱ屦矧舸梏盱┅ㄤ彐轭翦篝骈戾黩轸瀛翦篝骈戾Ⅳ弩舡蝈痫螋眢灬礅溽īㄦ矧遽汨痱弭豉痱轭啜ㄩ眇矧篦轶磲舂磲耘釉ㄧ镲洎ㄡ篌弪ｔ┅磲耘釉翎绫翎绮ㄡ篌弪ｆ┅磲耘釉īㄣ潋狈┅┅┅ㄤ彐轭轭泔眇戾翦换泸遽翦轭泔眇戾翦翦篝篚轸秕麴豸黩轸瀛翦篝骈戾㈤钽镯痨弭瀹眢盹灬礅溽ī戾ㄛ镳ㄣ躜蝈铘秕麴豸痫螋┹换脲屦轭簌钽鳢黩轸瀛礤翎溽翎轭磲舢篌麸狯镩屮痫螋牦镱瑚蜷翦镡赍泗镳ｆ牦镱瑚蜷翦圻豉疱㈨弭岘膂⑤垭妁泔眇戾翦洧埚犰蹂ｆ荸牦镱瑚蜷翦镡赍泗镳ｆ牦镱瑚蜷翦圻豉疱㈨弭岘膂⑤垭妁Ⅳ弩舡骈戾⑤埚犰蹂㈤钽镯痨弭瀹眢⑤┅┅ㄤ彐轭翦篝骈戾黩轸瀛翦篝骈戾Ⅳ弩舡蝈痫螋伯眢鲲殇┅ㄤ彐轭翦篝骈戾黩轸瀛翦篝骈戾Ⅳ弩舡蝈痫螋钞眢灬礅溽īㄦ矧遽汨痱弭豉痱轭啜ㄩ眇矧篦轶磲舂换蜥轶弪蝻鏖翳篝徙麸漉眇ㄤ彐轭ㄢ蹒瞟ㄩ癌ㄥ蝌矧р蹒溟漕栳钿戾弪铫ㄣ镱ㄢ蹒ō暴┅┅磲弪īㄡ篌弪扉篝ㄢ蹒卑┅┅┅┅ㄤ彐轭ㄣ桢汶蝈篚祠翦篝钺礤豉疱翎珞礤篌徵篝徙氅磲翥璀戾舄ㄛ眚ㄦ轭灬礅溽眚ㄥ聃犰翦篝钺礤磲舡蝈篚祠翦篝眚┅蝈篚祠螬郏磲舡蝈篚祠眚┹郜吏疱磲舡蝈篚祠豉疱眚┹郜吏徵磲舡蝈篚祠翎珞眚┹郜理弩筢珏磲舡蝈篚祠礤篌徵眚┹郜荔翎汶ㄡ钿篝徙磲舡蝈篚祠篝徙眚┅荸э氅ㄤ彐轭盹疳翳篝蜷铉狃疱钿翦篝骈戾盹┅ㄤ彐轭盹疳翳篝蜷铉狃疱钿翦篝骈戾盹┅ㄤ彐轭盹疳翳篝蜷铉狃疱钿翦篝骈戾盹┅ㄤ彐轭ㄧ弭溽翎泔铘孱舂磲翥璀戾舄ㄛì溽翎痱彗屮瓠磲翥⒓筱蜷痿决狎溽翎ó┗集筱蜷痿炯筱蜷痿拒骢钽糸镱弩汜疱若盱泔铘孱舂荸牦镱后趄轭绛撅怅邈溽翎┅ㄤ彐轭蝈篚祠蟓镦豉疱镡戛ㄦ殪翦灬礅溽颟ㄥ窨磲舡蝈篚祠豉疱颟┅磲舡溽翎蝈篚祠镡戛┅ㄤ彐轭ㄦ轭洵翦篝钺礤镡戛ㄦ轭灬礅溽颟ㄥ窨钺礤磲舡蝈篚祠翦篝颟┅磲舡溽翎蝈篚祠镡戛┅铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌铒铄蝈痫螋蝈痫螋骈戾翦篝骈戾Ж┅换汨邈仍吞蝈痫螋秕麴豸磲翥璀戾舄ㄛｔㄦ殪瀛屮轶趔蝈痫螋骈戾┹郜泔铘孱豸娓倔趄轭蝈徜骈戾蝈痫螋骈戾┅郇镡戛ㄧ弭溽翎泔铘孱舂郜翦篝杯蝈篚祠ㄦ轭洵翦篝г庞员镡戛郜翦篝箔蝈篚祠ㄦ轭洵翦篝г庞圆镡戛郜翦篝抄蝈篚祠ㄦ轭洵翦篝г庞猿镡戛邰疳篌牦镱候彐翦篝杯蝈篚祠豉疱ｆ┹邰驷殪牦镱候彐翦篝箔蝈篚祠豉疱ｆ┹郇擤痱彗屮瓠磲翥⑴沐痿轱詈驷殪邃狍箦螋轱ｆ狒扉铄超牦镱候彐翦篝箔蝈篚祠ы弩筢珏ｆ┅邰驷殪牦镱候彐翦篝抄蝈篚祠豉疱ｆ┹邰砒沐痿轱轭沅蚝狈轶铒疳轵牦镱候彐翦篝抄蝈篚祠ы弩筢珏ｆ┹э氅换汨邈盹蝈篚祠骈戾戾ㄛ溽翎祜徜蝈篚祠盹疳翳暴荸戾ㄛ蝈篚祠磲舡溽翎蝈篚祠溽翎┹ㄣ桢汶蝈篚祠г庞员ю狍Ж顼镤ㄣ桢汶蝈篚祠г庞圆ф衢Ж翎绫翎绮ㄦ矧磲⑴沐痿轱詈驷殪邃狍箦螋轱ｆ狒扉铄超汨狎巢镦幄翦篝骈戾ｆㄣ桢汶蝈篚祠г庞猿ф衢Ж⑴沐痿轱轭沅蚝狈轶铒疳轵ｆ┅磲翥璀戾舄ㄛ礤翎磲舡溽翎礤翎溽翎溽翎┹换礤翎溽翎泔眇戾翦洎郏牦镱候彐礤翎с镯痨弭邃ｆ┹换礤翎溽翎箫骠麽蝈轭骘郜殓铒蝈篦轶璀蝈鲩箝镱灬礅溽牦镱猴怅邈舡倔趄轭戾ㄛ泔瘗牦镱后趄轭绛撅怅邈牦镱猴怅邈舡倔趄轭┅荸牦镱后弭泔瘗Ж篦轶蝈鲩箝镱㈤珙矧邃泔瘗┅┹郜屮疱泗邃ㄩ珙矧瀛篦轶璀蝈鲩箝镱箫骠麽蝈轭骘┅郜厘疱泗邃ㄩ珙矧瀛篦轶璀蝈鲩箝镱牦镱候彐礤翎箫骠麽蝈轭骘ｆ┅换礤翎溽翎溽翦郜溽翦蝈珏ㄛ莴坜莴坜莴坜莴ㄛ藓莴┖ㄛ藓莴⑤郇屮瓠溽屮瓠栾躜屮瓠黹瞟痱彗屮瓠磲翥溽翦蝈珏ㄦ矧磲舡蜴悴覆ㄣ躜蝈铘溽翦┅┹郇徙舡溽徙舡栾躜徙舡黹瞟痱彗屮瓠磲翥溽翦蝈珏牦镱候彐礤翎т狒┅郏矧篝蜷铉娇屮瓠溽徙舡溽篝蜷铉娇徙舡栾躜⒉尝┅换礤翎溽翎糸礤篝犴皓郜翳孱牦镱候彐礤翎糸礤篝犴ㄥ痿洞┅郜铒ㄥ蜢犷绾铒鳗郏翳孱铒鳗换礤翎溽翎翦篝骈戾郜翦篝骈戾疳翳灬篝翦篝骈戾┹郜吏弩舡骈戾疳翳灬篝牦镱候彐礤翎翦篝骈戾㈡衢膦┅换礤翎溽翎翦篝蝓瞟郜翦篝蝓牦镱候彐礤翎翦篝蝓㈡衢膦┹埏ㄡ钿篝蜷铉觉蹰翦篝蝓瞟э氅换礤翎溽翎栾篝钺礤郜栾篝钺礤矬檫珏暨栾篝钺礤┹郜黎矬纛犴牦镱候彐礤翎ц矬纛犴ｆ┹换礤翎溽翎磲汨轭瀛豉疱郜狎汨磲汨轭瀛豉疱┹郜泪蜚篝蜷铉倔礅镬牦镱候彐礤翎ы徙栝铄豉疱┅荸э氅换箴邈殒屮痨殂轸貂殇戾ㄛ翦篝骈戾黩轸瀛翦篝骈戾㈠痨殂轸貂殇眢灬礅溽īㄦ矧遽汨痱弭豉痱轭啜ㄩ眇矧篦轶磲舂磲顼镤īㄡ篌弪ｔ┅磲忉īㄡ篌弪ｆ┅┅┅坼痨殂轸牦镱疳翳泔礅轭秕麴豸溟颟㈠痨殂轸牦镱┹埘蹰貂殇倔趄轭矬檫磲脲啧蹰洎┹铒铤弪锃屮轸篦轶璀翦篝翦篝蝓ㄦ矧磲狺貂殇蝈痫螋屮痨殂轸牦镱翦篝骈戾Ж箦咫⒃弩趔蝓詈嗅篌漆殪与轲阿┅磲翥璀戾舄ㄛì骈戾暴牦镱衡翦鲥泗矧撅怅邈蝈徜骈戾屮痨殂轸牦镱┅郜栗蹰牦镱候彐骈戾Ж礤翎溽翎翦篝蝓瞟ｆ┹э氅换汨邈篝徙骈屐铒铤弪锃屮轸篦轶璀翦篝翦篝骈戾畅Ж箦咫⑴沐痿轱轭怩绾溟漕栳钿戾弪铫┅戾舄ㄛ溽翎祜徜蝈篚祠盹疳翳畅垓弩蹯趔磲舡溽翎蝈篚祠溽翎┹ㄣ桢汶蝈篚祠弪ф衢Ж⑴沐痿轱轭怩绾溟漕栳钿戾弪铫ㄦ矧磲蝴＜泔铘轭踽糸镱轭怩缇狒扉铄惮汨狎钡镦彳詈彳铪郁徙漉眇趄躅汜翦漉麸磲溴痿卑茴磲灬礅溽瞟扉篝翦篝骈戾ǐ暴┅ㄩ雉卑┅┅换犰疳篌怩箫礤箅轲疱箫铒铤弪屮轸铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌铒铄翎顼镤蝈痫螋蝈痫螋骈戾翦篝骈戾Ж┅换汨邈仍吞蝈痫螋秕麴豸磲翥璀戾舄ㄛｔㄦ殪瀛屮轶趔蝈痫螋骈戾暴郜泔铘孱豸娓倔趄轭蝈徜骈戾蝈痫螋骈戾暴┹郇镡戛ㄧ弭溽翎泔铘孱舂郾戾铉翳蝈篚祠蟓镦豉疱ю狍镡戛┹鄄戾铉翳蝈篚祠蟓镦豉疱箅轲镡戛┹郯戾铉翳蝈篚祠蟓镦豉疱ф衢镡戛┹э氅换犰疳篌箫礤箅轲疱洮怩翦篝箴邈殒殄屮痨殂轸禊箫弪屮轸弪锃屮轸篦轶璀翦篝痱镧蝈篌铒铄翎顼镤蝈痫螋蝈痫螋骈戾翦篝骈戾耘釉雹Ж┅换汨邈仍吞蝈痫螋秕麴豸磲翥璀戾舄ㄛｔㄦ殪瀛屮轶趔蝈痫螋骈戾博郜泔铘孱豸娓倔趄轭蝈徜骈戾蝈痫螋骈戾博┹郇镡戛ㄧ弭溽翎泔铘孱舂郾戾铉翳蝈篚祠蟓镦豉疱ю狍镡戛┹郯戾铉翳蝈篚祠蟓镦豉疱箅轲镡戛┹郯戾铉翳蝈篚祠蟓镦豉疱ф衢镡戛┹э氅换箫礤箅轲疱箫铒铤弪屮轸铒蝈篚祠骘翦篝骈戾铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌铒铄翎顼镤蝈痫螋蝈痫螋骈戾翦篝骈戾翦篝骈戾博Ж┅换汨邈仍吞蝈痫螋秕麴豸磲翥璀戾舄ㄛｔㄦ殪瀛屮轶趔蝈痫螋骈戾畅郜泔铘孱豸娓倔趄轭蝈徜骈戾蝈痫螋骈戾畅┹郇镡瓯镡瓴ㄧ弭溽翎泔铘孱舂邰翦篝蝈痫螋眢疳翳灬篝牦镱候彐镡瓯Ж礤翎溽翎翦篝骈戾ｆ┅邰翦篝蝈痫螋伯眢疳翳灬篝牦镱候彐镡瓴Ж礤翎溽翎翦篝骈戾ｆ┅郾戾铉翳蝈篚祠蟓镦豉疱ю狍镡瓯┅鄄戾铉翳蝈篚祠蟓镦豉疱箅轲镡瓯┅郯戾铉翳蝈篚祠蟓镦豉疱ф衢镡瓯┅郜翦篝杯蝈篚祠ㄦ轭洵翦篝г庞员镡瓯┹邰疳篌牦镱候彐翦篝杯蝈篚祠豉疱ｆ┹郇牦镱候彐镡瓴蝈篚祠ｆ┹э氅换汨邈篚眄狎磲翥璀戾舄ㄛū畅ㄣ犰飙鏖翳鲠祯弩灬礅溽ī篚眄狎辁扉篝盹疳翳盹疳翳轭泔眇戾翦┅扉篝┹э氅换翦篝驷殪箫礤箅轲疱铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌铒铄铒顼镤蝈痫螋蝈痫螋骈戾翦篝骈戾Ж┅换汨邈仍吞蝈痫螋秕麴豸磲翥璀戾舄ㄛｔㄦ殪瀛屮轶趔蝈痫螋骈戾穿郜泔铘孱豸娓倔趄轭蝈徜骈戾蝈痫螋骈戾穿┹郇镡戛ㄧ弭溽翎泔铘孱舂郯戾铉翳蝈篚祠蟓镦豉疱ю狍镡戛┹郾戾铉翳蝈篚祠蟓镦豉疱箅轲镡戛┹鄄戾铉翳蝈篚祠蟓镦豉疱ф衢镡戛┹э氅换汨邈篚眄狎磲翥璀戾舄ㄛò博ㄣ犰飙鏖翳鲠祯弩灬礅溽ī篚眄狎辁扉篝盹疳翳盹疳翳博┅扉篝┹э氅戾ㄛ徵珧彗狒瀹牦镱疳翳泔礅轭秕麴豸溟颟⑨珑蝈玑翦牦镱┹坩珑蝈玑翦梏盱疳翳泔礅轭秕麴豸溟颟⑨珑蝈玑翦梏盱┹坩祠梏盱疳翳泔礅轭秕麴豸溟颟⑨祠梏盱┹垠蹴磲蝙秕麴豸Ж⒃弩趔蝓詈嗅篌漆殪与轲雹箦咫⒂镯翦篝篚轸溟铒泔眇戾翦┹换蝈蝓翦篝骈戾骘栳蝣弩翦篝蟋箅轲痖铉铒铤弪锃屮轸篦轶璀翦篝痱镧蝈篌铒铄铒翎绫翦篝骈戾Ж┅换栳蝣弩犰镱痱轭趔篚眄狎犷轭泔眇戾翦铒糸沐弪锃屮轸篦轶璀翦篝栳蝣弩盹疳翳盹疳翳盹疳翳轭泔眇戾翦篚眄狎秕麴豸换栳蝣弩骘蝈痫螋犰箫痱轭趔篚眄狎犷轭泔眇戾翦铒糸沐弪锃屮轸篦轶璀翦篝栳蝣弩盹疳翳盹疳翳盹疳翳轭泔眇戾翦蝈痫螋徵珧彗狒瀹牦镱篚眄狎秕麴豸换祜徜蝈篚祠犰镱痱轭趔篚眄狎犷轭泔眇戾翦铒糸沐弪锃屮轸篦轶璀翦篝祜徜蝈篚祠徵珧彗狒瀹牦镱篚眄狎秕麴豸换祜徜蝈篚祠犷蝈痫螋珏铄蜥糸铉仍吞秕麴豸痱轭趔筢礤篚眄狎弪锃屮轸篦轶璀翦篝祜徜蝈篚祠徵珧彗狒瀹牦镱蝈痫螋徵珧彗狒瀹梏盱ㄣ镱Ⅲ邋骈戾寒徵珧彗狒瀹梏盱篚眄狎秕麴豸┅换汜珏铄蜥翦筢礤仍吞蝈痫螋鲩栳蝣弩犷蝈痫螋弪锃屮轸篦轶璀翦篝栳蝣弩盹疳翳盹疳翳盹疳翳轭泔眇戾翦蝈痫螋犰舢梏盱ㄣ镱Ⅲ邋骈戾寒犰舢梏盱篚眄狎秕麴豸┅换汨邈翳仍吞蝈痫螋秕麴豸磲翥璀戾舄ㄛ屮疱泗邃蝈徜骈戾徵珧彗狒瀹梏盱┹郜换麇珏翳筢礤仍吞秕麴豸怙翳麽ㄡ篌弪ㄥ聃犰屮疱泗邃蝈徜骈戾犰舢梏盱┅┹郜泔铘孱豸娓倔趄轭屮疱泗邃┹郜磲舡溽翎ㄧ弭溽翎泔铘孱舂郜骈钿篚轸灬礅溽翦篝骈戾ㄦ轭灬礅溽ㄥ聃犰翦篝骈戾牦镱候彐磲舡溽翎礤翎溽翎翦篝骈戾ｆ┅磲舡溽翎┅郜盹ㄦ轭洵篚轸翦篝骈戾┹郜翦篝杯蝈篚祠ㄦ轭洵翦篝г庞员盹暴郜翦篝箔蝈篚祠ㄦ轭洵翦篝г庞圆盹暴郜翦篝抄蝈篚祠ㄦ轭洵翦篝г庞猿盹暴邰疳篌牦镱候彐翦篝杯蝈篚祠豉疱ｆ┹邰箅轲牦镱候彐翦篝箔蝈篚祠豉疱ｆ┹邰驷殪牦镱候彐翦篝抄蝈篚祠豉疱ｆ┹郜盹ㄦ轭洵篚轸翦篝骈戾博郇磲舡溽翎蝈篚祠盹博郜盹ㄦ轭洵篚轸翦篝骈戾畅郜弪锃蝈篚祠ㄦ轭洵翦篝弪盹畅邰驷殪牦镱候彐弪锃蝈篚祠豉疱ｆ┹邰砒沐痿轱轭怩绾溟漕栳钿戾弪铫牦镱候彐弪锃蝈篚祠ы弩筢珏ｆ┹郇擤痱彗屮瓠磲翥泔铘轭踽糸镱轭怩绠狒扉铄惮汨狎钡牦镱候彐弪锃蝈篚祠篝徙㈩篝徙擘┅郜盹ㄦ轭洵篚轸㈤钽镯痨弭瀹眢┹郏牦镱候彐盹Ж礤翎溽翎泔眇戾翦洎⑩躔腴螈┹郇磲舡溽翎蝈篚祠盹穿荸э氅换趄栳蝣弩糸铉骝镯翳黩镱痨徙铒铤弪锃屮轸篦轶璀翦篝栳蝣弩盹疳翳蝈痫螋骈戾暴ЖⅢ鏖箬翦篝汜铑雉祜徜磲蝈篚祠骝镯蝈痫螋碑梏盱疹屮疱泗邃轭瘐狒痫箝糸镱换趄祜徜轭蝈篚祠骝镯翳黩镱痨徙铒铤弪锃屮轸篦轶璀翦篝祜徜蝈篚祠蝈痫螋骈戾暴ЖⅢ鏖箬翦篝汜铑雉祜徜蝈篚祠骝镯蝈痫螋碑梏盱疹屮疱泗邃轭瘐狒痫箝糸镱ㄩ箫灬翦磲轭泔眇戾翦īㄤ彐轭徕矧舢眢黩轸瀛翦篝骈戾⑨怙螋眢灬礅溽ī痱弭豉痱轭Ж徕矧舂┅┅ㄤ彐轭铒翦篝螽眢黩轸瀛翦篝骈戾㈩锃翦篝螽眢鲲殇┅ㄤ彐轭疳篌眢黩轸瀛翦篝骈戾疳篌眢灬礅溽īㄦ矧遽汨痱弭豉痱轭啜ㄩ眇矧篦轶磲舂磲顼镤īㄡ篌弪ｔ┅┅┅铒铤弪锃屮轸换泔眇灬轭殒翦篝篚轸滹弩铒泔眇戾翦篦轶璀翦篝徕矧舢眢啜⒂镯翦篝篚轸溟铒泔眇戾翦┅铒铤弪锃屮轸换泔眇灬轭殒翦篝篚轸滹弩铒泔眇戾翦篦轶璀翦篝徕矧舢眢铒翦篝螽眢啜箦咫⒃弩趔蝓詈嗅篌漆殪与轲阿箦咫⒂镯翦篝篚轸溟铒泔眇戾翦箦咫⑽翦篝骘躅轭㈩锃翦篝螽眢┅铒铤弪锃屮轸换泔眇灬轭殒翦篝篚轸滹弩铒泔眇戾翦篦轶璀翦篝徕矧舢眢铒翦篝螽眢疳篌眢啜箦咫⒃弩趔蝓詈嗅篌漆殪与轲阿箦咫⒂镯翦篝篚轸溟铒泔眇戾翦箦咫⑽翦篝骘躅轭㈩锃翦篝螽眢┅换汨邈篚眄狎磲翥璀戾舄ㄛū畅ㄣ犰飙鏖翳鲠祯弩灬礅溽ī篚眄狎辁磲灬礅溽ㄦ瞟篝蜷铉狃疱钿骖盹┅扉篝徕矧舢眢疳篌眢铒翦篝螽眢┅┅扉篝┹э氅换汨邈狯衢灬忪礤翎溽翎骘轭泔眇戾翦盹磲翥璀戾舄ㄛ徕矧舡蝈篚祠祜徜蝈篚祠篝蜷铉狃疱钿徕矧舢眢盹┅郇磲舡溽翎蝈篚祠徕矧舡蝈篚祠┹郜徕矧舡礤翎磲舡溽翎礤翎溽翎徕矧舡蝈篚祠┹换礤翎溽翎泔眇戾翦洎郏牦镱候彐徕矧舡礤翎с镯痨弭邃镲痼┹换礤翎溽翎翦篝骈戾郜屮疱泗邃疳翳灬篝徕矧舢眢┹郜厘疱泗邃疳翳灬篝牦镱候彐徕矧舡礤翎翦篝骈戾┅换礤翎溽翎栾篝钺礤郜栾篝钺礤矬檫珏暨栾篝钺礤┹郜黎矬纛犴牦镱候彐徕矧舡礤翎ц矬纛犴ｆ┹换礤翎溽翎磲汨轭瀛豉疱郜狎汨簌礅镬倔趄轭磲汨轭瀛豉疱┅郜泪蜚牦镱候彐徕矧舡礤翎ы徙栝铄豉疱ｆ┹э氅ㄩ箫灬翦磲骈戾屮翦铙轱īㄤ彐轭翦篝骈戾黩轸瀛翦篝骈戾㈩锃翦篝螽眢鲲殇┅ㄤ彐轭箫躜沐骈戾黩轸瀛翦篝骈戾㈩锃翦篝螽篌鲲殇┅铒铤弪锃屮轸换泔眇灬轭殒眢骈戾泔铘衢铙铒翦篝篦轶璀翦篝翦篝骈戾啜箦咫⒃弩趔蝓詈嗅篌漆殪与轲阿箦咫⑽翦篝骘躅轭痱彗屮瓠聃雉翦篝骈戾┅弪锃屮轸换滹瞌泔眇灬轭殒铒瞽眢骈戾泔铘衢铙铒翦篝篦轶璀翦篝箫躜沐骈戾Ж┅铒铤弪锃屮轸篦轶璀翦篝蝈痫螋骘镡狎Ж汜铑雉溴翦蝽轭秕麴豸骘蝽狒骝镯蝈痫螋骘镡狎屮疱泗邃牦镱梏盱矧梏屮翦铙轱睥换族箬豸滹黝翳痱镦殪弪翦眇矧狎殪轭箫礤痨徙弩麸狯镩轭翦蜴弪轭换鏖翳翳痱镦殪搴篝狎汜祆轭篦轶璀翦篝翳轶礤犷翳泔鲥蜥珏蝈痫螋换躅溴蝮翎翦翳徙趱犰泔鲥蜥珏骘篦轶璀翦篝ㄤ彐轭瀛簌铘狲鏖翳秕舡痱镦殪弪簌铘狲蝓戾ī郇灏灞戾舄ㄛ痖麒弪彘ю蝻骈戾颟垧骈戾ㄡ钿痖痱镦殪搴骈戾钺礤┅荸麒孱痖痱镦殪搴篝镳┅镱屮轸戾ㄛ痖麒弪彘ю蝻骈戾颟荸麒孱痖痱镦殪搴篝镳┅麒孱疰殪磲翥痱镦殪搴篝狎疰殪疰殪ｔ郏镫痖洎鲲殇┹┅灏灞┅荸ㄩ箫灬翦磲痱镦殪īㄤ彐轭箫躜沐骈戾黩轸瀛翦篝骈戾痱镦殪瀛泔溴篌灬礅溽īㄤ轶痨狴！汨妃筱桢礤茴ㄤ轶痨狴ㄤ彐轭ㄦ徙瞟茴ㄤ轶痨狴ㄩ癌茴ㄤ轶痨狴避睥ㄤ轶痨狴íㄦ徙ō暴┅┅茴ㄤ轶痨狴ㄤ彐轭躅躞邃┸睥ㄤ轶痨狴麒孱ㄦ禊ю殓螬茴ㄤ轶痨狴躅躞邃┅┸睥ㄤ轶痨狴！屣磲趔茴ㄦ矧遽汨痱弭豉痱轭Жㄩ眇矧篦轶磲舂篦轶翦篝轭绌换蝈屮痫箦岈麒殂轶犰蝈徜屮痫螋邃祜徜翳轶磲翦篝īㄡ篌弪镤淇ㄦ徙癌┅磲翦篝īㄡ篌弪辈ㄦ徙旦┅┅┅┅ㄤ彐轭箫躜沐骈戾蝈疳翳蝈灬糸鲥箫躜沐骈戾┅ㄤ彐轭翦篝骈戾黩轸瀛翦篝骈戾痱镦殪瀛泔溴眢灬礅溽īㄦ矧遽汨痱弭豉痱轭啜ㄩ眇矧篦轶磲舂篦轶痱镦殪濠篦轶翦篝轭绌祜徜箫躜沐骈戾蝈飑磲岜īㄡ篌弪镤淇ㄦ徙癌┅ㄡ篌弪ㄥ鲥羁ㄦ徙卑┅┅┅┅ㄤ彐轭痱镦殪瀛骈戾疳翳泔礅轭秕麴豸溟颟痱镦溽簪┅ㄤ彐轭痱镦殪灞骈戾疳翳泔礅轭秕麴豸溟颟痱镦碑溽簪┅ㄤ彐轭痱镦殪宀骈戾疳翳泔礅轭秕麴豸溟颟痱镦伯溽簪┅ㄤ彐轭徵珧彗狒瀛骈戾疳翳泔礅轭秕麴豸溟颟⑨珑溽簪┅ㄤ彐轭泔鲥蜥珏骈戾疳翳泔礅轭秕麴豸溟颟泔觫Ⅲ蹴磲蝙梏盱┅ㄤ彐轭泔鲥蜥珏杯骈戾疳翳泔礅轭秕麴豸溟颟泔霰Ⅲ蹴磲蝙梏盱┅ㄤ彐轭ㄣ桢汶泔躅泔躅屐螬痱彗屮瓠磲翥ㄦ矧磲⒓箴犷糸綮褰堍彳③蘧莳峻峒箴犷劲泔躅痱彗屮瓠聃雉屐舂螬ㄤ彐轭篚眄狎蝻骈戾钺礤栝趔箝翦泔鲥蜥珏磲泔躅舂ㄦ矧磲岙剪渚峒翡炯翡峻峒翡井幞剪渚峒翡劲痱彗屮瓠聃雉痱彗屮瓠蝈痨徙濯④苘堍骈戾钺礤┅栝趔箝翦泔鲥蜥珏磲泔躅舂换筢鲥痱镦殪秭弪黩轸弩徙沲眭灬翦镱禊鏖翳屮痨殂轸祜徜痱镦殪ㄤㄛǐ暴荸è博鏖翳秕舡痱镦殪弪弪锃屮轸篦轶璀翦篝筢鲥痱镦殪痱镦殪瀛骈戾痱镧蝈篌篚轸翦篝骈戾扉篝ㄦ矧磲疳篌痱彗屮瓠聃雉翦篝骈戾┅┅弪锃屮轸篦轶璀翦篝祜徜痱镦殪痱镦殪瀛骈戾泔鲥蜥珏泔鲥蜥珏骈戾Ж箦咫换铄邃邃骘磲脲泔鲥蜥珏漉麸⒂腴痧轭Ⅲ邋骈戾寒泔霪篚眄狎梏盱⒃弩趔蝓詈嗅篌漆殪与轲阿┅磲翥璀戾舄ㄛｔㄦ殪瀛蝈珲灬蚩泔鲥蜥珏骈戾┹郜泔豸娓倔趄轭蝈徜骈戾泔鲥蜥珏骈戾┅郇擤痱彗屮瓠磲翥⑾鲥蜥祆反泔鲥蜥珏鏖翳狈镦渤箝翦泔鲥蝈洧泔雯郇擤痱彗屮瓠磲翥篚眄狎蝻箫躜沐骈戾蝈狈渤反辈泔雯郜篌梏盱疳翳泔礅轭疳翳疳蝈铘泔鲥蜥珏骈戾篝蜷铉狃疱钿箫躜沐骈戾蝈梏盱┅郏ㄦ殪瀛蝈珲灬蚩篌梏盱┹郜篌豸娓倔趄轭蝈徜骈戾篌梏盱┅郇擤ㄣ桢汶泔躅⒈篌┹换忉箦汜箦郇擤ㄣ桢汶泔躅卑篌┹换蝈沲蝮轹汜祆郇擤ㄣ桢汶泔躅Ⅴ铛箦洧篌┹э氅换筢鲥痱镦殪泔礅轭邃鏖翳泔鲥蜥珏骘箴邈殒殂！屣磲鏖翳秕舡痱镦殪弪弪锃屮轸篦轶璀翦篝筢鲥痱镦殪痱镦殪灞骈戾泔鲥蜥珏泔鲥蜥珏杯骈戾箫躜沐骈戾蝈翦篝阿Ж箦咫Ⅲ邋骈戾寒泔霰篚眄狎梏盱⒃弩趔蝓詈嗅篌漆殪与轲阿┅磲翥璀戾舄ㄛｔㄦ殪瀛蝈珲灬蚩泔鲥蜥珏杯骈戾┹郜泔霰豸娓倔趄轭蝈徜骈戾泔鲥蜥珏杯骈戾┅郇擤痱彗屮瓠磲翥⑾鲥蜥祆吹泔鲥蜥珏鏖翳备镦窗箝翦泔鲥蝈洧泔霰┹郇擤痱彗屮瓠磲翥篚眄狎蝻箫躜沐骈戾蝈备窗吹暴泔霰┹郜篌梏盱疳翳泔礅轭疳翳疳蝈铘泔鲥蜥珏杯骈戾篝蜷铉狃疱钿箫躜沐骈戾蝈梏盱┅郏ㄦ殪瀛蝈珲灬蚩篌梏盱┹郜篌豸娓倔趄轭蝈徜骈戾篌梏盱┅郇擤ㄣ桢汶泔躅⒈篌┹换忉箦汜箦郇擤ㄣ桢汶泔躅篌┹换蝈沲蝮轹汜祆郇擤ㄣ桢汶泔躅Ⅴ铛箦洧篌┹郇擤ㄣ桢汶泔躅镤淇篌┹换蜥翦篝郇擤ㄣ桢汶泔躅⒈舶篌┹换溟铒蝓翦篝э氅换筢鲥痱镦殪骘祆秣邃怡箦疳蜥翦泔鲥蜥珏骘溟骀弪孱！屣磲鏖翳秕舡痱镦殪弪弪锃屮轸篦轶璀翦篝筢鲥痱镦殪痱镦殪宀骈戾箫躜沐骈戾蝈翦篝耽Ж箦咫疳篌翦篝耽箦咫⒃弩趔蝓詈嗅篌漆殪与轲阿┅弪锃屮轸篦轶璀翦篝祜徜痱镦殪痱镦殪宀骈戾泔鲥蜥珏泔鲥蜥珏杯骈戾Ж箦咫换铄邃邃骘磲脲泔鲥蜥珏漉麸⒂腴痧轭Ⅲ邋骈戾寒泔霰篚眄狎梏盱⒃弩趔蝓詈嗅篌漆殪与轲阿┅磲翥璀戾舄ㄛｔㄦ殪瀛蝈珲灬蚩泔鲥蜥珏杯骈戾┹郜泔霰豸娓倔趄轭蝈徜骈戾泔鲥蜥珏杯骈戾┅郇擤痱彗屮瓠磲翥⑾鲥蜥祆钒泔鲥蜥珏鏖翳哺镦窗箝翦泔鲥蝈洧泔霰┹郇擤痱彗屮瓠磲翥篚眄狎蝻箫躜沐骈戾蝈哺窗钒订泔霰┹郜篌梏盱疳翳泔礅轭疳翳疳蝈铘泔鲥蜥珏杯骈戾篝蜷铉狃疱钿箫躜沐骈戾蝈梏盱┅郏ㄦ殪瀛蝈珲灬蚩篌梏盱┹郜篌豸娓倔趄轭蝈徜骈戾篌梏盱┅郇擤ㄣ桢汶泔躅⒈篌┹换忉箦汜箦郇擤ㄣ桢汶泔躅篌┹换蝈沲蝮轹汜祆郇擤ㄣ桢汶泔躅Ⅴ铛箦洧篌┹郇擤ㄣ桢汶泔躅镤淇篌┹换溟铒蝓翦篝郇擤ㄣ桢汶泔躅⒈舶篌┹换蜥翦篝э氅换祜徜痱镦殪眭祠轲戾痱镦殪弩鲩泔鲥蜥珏弪锃屮轸篦轶璀翦篝祜徜痱镦殪痱镦殪灞骈戾痱镦殪宀骈戾泔鲥蜥珏泔鲥蜥珏杯骈戾Ж箦咫换铄邃邃骘磲脲泔鲥蜥珏漉麸⒂腴痧轭Ⅲ邋骈戾寒泔霰篚眄狎梏盱⒃弩趔蝓詈嗅篌漆殪与轲阿┅磲翥璀戾舄ㄛｔㄦ殪瀛蝈珲灬蚩泔鲥蜥珏杯骈戾┹郜泔霰豸娓倔趄轭蝈徜骈戾泔鲥蜥珏杯骈戾┅郇擤痱彗屮瓠磲翥⑾鲥蜥祆傅泔鲥蜥珏鏖翳炒镦窗箝翦泔鲥蝈洧泔霰┹郇擤痱彗屮瓠磲翥篚眄狎蝻箫躜沐骈戾蝈炒窗傅珐泔霰┹郜篌梏盱疳翳泔礅轭疳翳疳蝈铘泔鲥蜥珏杯骈戾篝蜷铉狃疱钿箫躜沐骈戾蝈梏盱┅郏ㄦ殪瀛蝈珲灬蚩篌梏盱┹郜篌豸娓倔趄轭蝈徜骈戾篌梏盱┅郇擤ㄣ桢汶泔躅⒈篌┹换忉箦汜箦郇擤ㄣ桢汶泔躅篌┹换蝈沲蝮轹汜祆郇擤ㄣ桢汶泔躅Ⅴ铛箦洧篌┹郇擤ㄣ桢汶泔躅镤淇篌┹换蜥翦篝郇擤ㄣ桢汶泔躅⒈舶篌┹换蜥翦篝э氅换徜麸痱镦殪鲩祜徜痱镦殪犷筢鲥痱镦殪犷筢鲥泔鲥蜥珏蝈痫螋鏖翳秕舡痱镦殪弪弪锃屮轸篦轶璀翦篝祜徜痱镦殪痱镦殪瀛骈戾筢鲥痱镦殪痱镦殪瀛骈戾泔鲥蜥珏泔鲥蜥珏骈戾翦篝骈戾箫躜沐骈戾蝈飑Ж箦咫疳篌岜③莴⒃弩趔蝓詈嗅篌漆殪与轲阿箦咫疳篌翦篝阿疳篌翦篝耽③莴⒃弩趔蝓詈嗅篌漆殪与轲阿箦咫Ⅲ邋骈戾寒泔霪篚眄狎梏盱⒃弩趔蝓詈嗅篌漆殪与轲阿┅磲翥璀戾舄ㄛｔㄦ殪瀛蝈珲灬蚩泔鲥蜥珏骈戾┹郜泔豸娓倔趄轭蝈徜骈戾泔鲥蜥珏骈戾┅郇擤痱彗屮瓠磲翥⑾鲥蜥祆傅泔鲥蜥珏鏖翳炒镦窗箝翦泔鲥蝈洧泔雯郇擤痱彗屮瓠磲翥篚眄狎蝻箫躜沐骈戾蝈炒窗傅潮泔雯郜篌梏盱疳翳泔礅轭疳翳疳蝈铘泔鲥蜥珏骈戾篝蜷铉狃疱钿箫躜沐骈戾蝈梏盱┅郏ㄦ殪瀛蝈珲灬蚩篌梏盱┹郜篌豸娓倔趄轭蝈徜骈戾篌梏盱┅郇擤ㄣ桢汶泔躅ǐí博博⒈篌┹换忉箦汜箦郇擤ㄣ桢汶泔躅ǐí卑旦篌┹换蝈沲蝮轹汜祆郇擤ㄣ桢汶泔躅Ⅴ铛箦洧篌┹э氅换礤蜱痱镦殪溽翎鲩祜徜痱镦殪犷筢鲥痱镦殪鏖翳秕舡痱镦殪弪弪锃屮轸篦轶璀翦篝祜徜痱镦殪痱镦殪瀛骈戾痱镦殪灞骈戾痱镦殪宀骈戾筢鲥痱镦殪痱镦殪瀛骈戾泔鲥蜥珏泔鲥蜥珏骈戾Ж箦咫换铄邃邃骘磲脲泔鲥蜥珏漉麸⒂腴痧轭Ⅲ邋骈戾寒泔霪篚眄狎梏盱⒃弩趔蝓詈嗅篌漆殪与轲阿┅磲翥璀戾舄ㄛｔㄦ殪瀛蝈珲灬蚩泔鲥蜥珏骈戾┹郜泔豸娓倔趄轭蝈徜骈戾泔鲥蜥珏骈戾┅郇擤痱彗屮瓠磲翥⑾鲥蜥祆傅泔鲥蜥珏鏖翳炒镦窗箝翦泔鲥蝈洧泔雯郇擤痱彗屮瓠磲翥篚眄狎蝻箫躜沐骈戾蝈炒窗傅ǐ潮珐泔雯郜篌梏盱疳翳泔礅轭疳翳疳蝈铘泔鲥蜥珏骈戾篝蜷铉狃疱钿箫躜沐骈戾蝈梏盱┅郏ㄦ殪瀛蝈珲灬蚩篌梏盱┹郜篌豸娓倔趄轭蝈徜骈戾篌梏盱┅郇擤ㄣ桢汶泔躅ǐí博í博⒈篌┹换忉箦汜箦郇擤ㄣ桢汶泔躅ǐí卑í旦篌┹换蝈沲蝮轹汜祆郇擤ㄣ桢汶泔躅Ⅴ铛箦洧篌┹э氅ㄩ箫灬翦磲扉怛狎īㄤ彐轭扉猸溟Ⅴ糸膦ㄤ彐轭扉猸骈戾黩轸瀛翦篝骈戾疳翳泔礅轭扉猸溟㈣屐甬篌灬礅溽ī痱弭豉痱轭啜扉怛狎ㄨ屐皓ㄥ痫螋镡ㄩ眇矧筱桢礤┅ㄤ彐轭镡暴┅┅ㄤ彐轭篁悱骈戾黩轸瀛翦篝骈戾㈧殁篁惝篌灬礅溽īㄦ矧遽汨痱弭豉痱轭啜ㄩ眇矧ì篝蜷铉倔礅镬疳翳蝻雉疳翳灬篝扉猸骈戾┅┅ㄩ眇矧筱桢礤┅ㄤ彐轭ㄧ蹂篌ㄩㄥ聃犰镡弩ь铹┅┅┅ㄤ彐轭翦篝骈戾黩轸瀛翦篝骈戾㈧殁翦篝眢灬礅溽īㄦ矧遽汨痱弭豉痱轭啜ㄩ眇矧篦轶磲舂篦轶痱镦殪濠篦轶翦篝轭绌祜徜篁悱骈戾磲夤ī磲翥璀戾舄ㄛ弩ㄧ蹂篌暴垲ㄧ蹂篌癌荸э氅┅┅┅弪锃屮轸篦轶璀翦篝疳翳泔礅轭秕麴豸溟颟扉猸溟颟翦篝骈戾Ж箦咫疳篌夤箦咫⒃弩趔蝓詈嗅篌漆殪与轲阿┅铒铤弪锃屮轸篦轶璀翦篝翦篝骈戾磲痱彗屮瓠聃雉扉篝⑴沐痿轱詈扉怛狎ㄨ屐皓铒骘躅洧ㄦ矧磲⒃弩漆殪邃幄翦篝骈戾┅┅